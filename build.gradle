/*
 *
 * DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE
 * Version 2, December 2004
 *
 * Copyright (C) 2021 Cephetir
 *
 * Everyone is permitted to copy and distribute verbatim or modified
 * copies of this license document, and changing it is allowed as long
 * as the name is changed.
 *
 * DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE
 * TERMS AND CONDITIONS FOR COPYING, DISTRIBUTION AND MODIFICATION
 *
 *  0. You just DO WHAT THE FUCK YOU WANT TO.
 */

buildscript {
    repositories {
    	mavenCentral()
        gradlePluginPortal()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
        maven { url "https://repo.sk1er.club/repository/maven-public" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath "com.github.Skytils:ForgeGradle:6f5327"
        classpath 'com.github.LxGaming:MixinGradle:d75e32e'
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "org.spongepowered.mixin"

version = "2.5"
group = "me.cephetir.skyskipped"
archivesBaseName = "SkySkipped"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
}

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    mavenCentral()
    maven { url = "https://repo.sk1er.club/repository/maven-public/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
}

dependencies {
    include('gg.essential:loader-launchwrapper:1.1.3')
    include('com.github.jagrosh:DiscordIPC:e29d6d8') {
        exclude module: 'log4j'
    }

    implementation('gg.essential:essential-1.8.9-forge:1655+release-launch')
    implementation('org.projectlombok:lombok:1.18.22')
    annotationProcessor('org.projectlombok:lombok:1.18.22')
    include('org.spongepowered:mixin:0.8.5-SNAPSHOT')
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    implementation(files("libs/dungeonsguide-3.7.3.jar"))
    implementation(files("libs/Skytils-1.0.9-RC2.jar"))
}

mixin {
    add sourceSets.main, "mixins.sm.refmap.json"
    defaultObfuscationEnv = searge
}

jar {
    enabled = false
}

sourceJar {
    enabled = false
}

shadowJar {
    archiveFileName = jar.archiveFileName

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    configurations = [project.configurations.include]

    manifest.attributes(
            "ForceLoadAsMod": true,
            'ModSide': 'CLIENT',
            "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
            "TweakOrder": "0",
            'MixinConfigs': "mixins.sm.json",
    )
}

reobfJar.dependsOn tasks.shadowJar

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    from(file("LICENSE"))
}
